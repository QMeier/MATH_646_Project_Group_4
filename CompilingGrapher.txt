Manipulate[
 If[ct, DensityPlot, Plot3D][
  Evaluate[Abs@u[If[ct, x0 - t, t], x] /. 
    Quiet[NDSolve[
      Evaluate[{I D[u[t, x], t] + D[u[t, x], x, x] + 
          2 Abs[u[t, x]]^2 u[t, x] == 0, 
        u[0, x] == E^-(x - a)^2 + E^-(x + a)^2, 
        u[t, -x0] == u[t, x0]}], u, {t, 0, x0}, {x, -x0, x0}, 
      Method -> {"MethodOfLines", 
        "SpatialDiscretization" -> {"TensorProductGrid", 
          "DifferenceOrder" -> "Pseudospectral", 
          "MinStepSize" -> 0.2`}}]]], {x, -x0, x0}, {t, 0, 10}, 
  Mesh -> mesh, MeshFunctions -> {#3 &}, 
  ColorFunctionScaling -> False, PlotRange -> Full, 
  ColorFunction -> Hue, PlotPoints -> 30, 
  MaxRecursion -> ControlActive[1, 2], ImageSize -> {475, 325}, 
  ImagePadding -> 15],
 {{a, 1., "initial peak separation"}, 0, 10, Appearance -> "Labeled"},
 {{x0, 5., "solution range"}, 1, 50, 
  Appearance -> "Labeled"}, {{mesh, Automatic, "show mesh"}, {False, 
   Automatic}, 
  ControlType -> Checkbox}, {{ct, True, "plot type"}, {True -> "2D", 
   False -> "3D"}, ControlType -> SetterBar}, 
 ControllerLinking -> True, ControlPlacement -> Top,
 SynchronousInitialization -> False, 
 SynchronousUpdating -> False]